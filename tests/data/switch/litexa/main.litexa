define @thing as Thing

launch 
  local x = 1 + 2
  local y = ( 1 + 3 ) / 3
  x = sumNumbers( 1, 2, 3, x )
  log x
  local result = "win"

  switch result
    == "win" then
      ok("you won!")
      say "yay!"
    == "lose" then
      fail("you lose.")

  switch something = 12
    < 5 then
      fail("few")
    < 10 then
      fail("some")
    else
      ok("plenty!")

  switch something = Math.random()
    <0.5 then
      ok("low")
    else
      ok("high")

  switch a=7, b=5, c=3
    == 1 then
      fail(b + c)
    a > b and c == 3 then
      ok("a, b and c")
    else
      fail()

  switch a = 4, b = 10
    a == b then
      fail()
    a < b then
      ok("smaller")
    a > b then
      fail()

  switch a = pickSomething("do", 4), b = true
    a == 4 and b then
      ok(a / 2)
    ( a == 6 ) then
      fail()
    == a - b then
      fail()
    else
      fail()

  switch a = 8
    match /dog|cat|bear/i then
      fail("not a dog, cat or bear")
    match /4|8/ig then
      ok("just eight")
    else
      fail("not an 8")

  @test = 36
  say "@test / 4 = { @test / 4 } "
  switch @test
    == 36 then
      ok(@test)
      -> intermediate
    >= 15 then
      fail()
      -> goodbye
    == 4 then
      fail()
      -> goodbye
    else
      fail()
      -> goodbye


  # nested switches
  switch 40
    < 10 then
      fail()
    > 30 then
      switch 10
        > 30 then
          fail()
        == 10 then
          ok("nested == 10")


  # switching on a function call result
  switch getDoormouse()
    == "doormouse" then
      ok("Doormouse")
    else
      fail("not doormouse")

  # and on a class
  switch @thing.child.get37()
    < 30 then
      fail()
    > 40 then
      fail()
    == 37 then
      ok("got 37 from thing's child")

  # switch on nothing
  switch
    < 10 then
      fail()
    getDoormouse() == "doormouse" then
      ok("got doormouse from nothing")
    else
      fail("coulnt' get something from nothing")


intermediate
  say "went through intermediate"
  -> goodbye

goodbye
  END


TEST "run through"
  launch
  alexa: null
